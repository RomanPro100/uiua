# Stack macros
F! ← ^!5
G! ← F!^!
⍤⟜≍: ¯5 G!¯
Four! ← ∩∩^!
⍤⟜≍: [4 6 8 10] [Four!(×2) 2 3 4 5]
Dyad‼ ← ^!^!
⍤⟜≍: 12 Dyad‼(×2|+1) 5
F‼‼ ← ^!^!^:^!^!^.^!^!^,
⍤⟜≍: [2 1 3 3 4 3] F‼‼(⊂1|⊂2|⊂3|⊂4) []

A! ← setinv(^!|°^!)^.
B! ← setinv(°^!|^!)^.
C ← setinv(∘|°A!utf)
⍤⟜≍: "é" °A!utf [195 169]
⍤⟜≍: [195 169] °B!utf "é"
⍤⟜≍: "é" °C [195 169]

F! ← (
  ^.
  ^!^!
)
⍤⟜≍: 6 F!+ 1 2 3
F! ← [^!^!
      ^.]
⍤⟜≍: [6] F!+ 1 2 3
F! ← ⟨
  ^!
| ^!^.
⟩
⍤⟜≍: ¯5 F!¯ 0 5

# Array macros
F! ←^
G! ←^ ⇌
H‼ ←^ ⊂⊢.
⍤⟜≍: [1 2] F!(⊂1|⊂2) []
⍤⟜≍: [2 1] G!(⊂1|⊂2) []
⍤⟜≍: [1 1 2] H‼(⊂1|⊂2) []
⍤⟜≍: [1 1 2] H‼(⊂1)(⊂2) []

X ← 1
F! ←^ ⊂□°⋕X
⍤⟜≍: [1 2] [F!2]

F‼ ←^ $"_ ← _\n" °⊟
F‼Y5
⍤⟜≍: 5 Y

Foo! ←^ "t ←rand" ◌
Hi ← Foo!+"a"
⍤⟜≍: "a" Hi

a ← 4
b ← (⚂)
c ← comptime(⚂)
d ← gen ⚂
e ← ◌ gen 1

A! ←^ $"_" a ◌
B! ←^ $"_" b ◌
C! ←^ $"_" c ◌
D! ←^ $"_" ◌ d ◌
E! ←^ $"_" e ◌

◌A!1
◌B!2
◌C!3
◌D!4
◌E!5
